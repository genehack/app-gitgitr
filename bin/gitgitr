#! /opt/perl/bin/perl

use strict;
use warnings;
use 5.010;

use Archive::Extract;
use Carp;
use File::Remove 'remove';
use LWP::Simple;

my $version = shift || get_current_version();

unless ( -e "/opt/git-$version" ) {
  say "BUILD/INSTALL git-$version";

  say "*** download";
  my $pkg = sprintf "git-%s.tar.gz" , $version;
  my $url = sprintf "http://kernel.org/pub/software/scm/git/%s" , $pkg;
  getstore( $url , $pkg );

  say "*** extract";
  my $ae = Archive::Extract->new( archive => $pkg );
  $ae->extract or die $ae->error;
  unlink $pkg;

  say "*** configure";
  chdir "git-$version";
  system( "./configure --prefix=/opt/git-$version 2>&1 >/dev/null" );

  say "*** make";
  system( "make 2>&1 >/dev/null" );

  say "*** make test";
  #system( "make test 2>&1 >/dev/null" );

  say "*** make install";
  system( "make install 2>&1 >/dev/null" );

  say "*** cleanup";
  chdir '..';
  remove( \1 , "git-$version" );

  say "\n\nBuilt new git $version.";

}

say "*** symlink";
chdir '/opt';
remove( 'git' );
symlink( "git-$version" , 'git' );

say "New version symlinked into /opt/git";

sub get_current_version {
  my $content = get( 'http://git-scm.com/' );

  my( $version ) = $content =~ m|<div id="ver">v([\d\.]+)</div>|
    or croak "Can't parse version from Git web page!";

  return $version;
}
